#!/bin/csh

source common.build

module purge

set deps = "${gcc_mod} ${omp_mod} ${fftw_mod} ${python_mod} ${numpy_mod}"
foreach dep ( $deps )
  module load $dep
end
module load ${git_mod}

source build.environ

set module = "sigpyproc"
set source_dir = "${source_base}/${module}"

if (! -d $source_dir) then
  ssh ${ssh_loc} "cd $source_base; git clone https://github.com/ewanbarr/sigpyproc.git"
endif

pushd $source_dir

# If not hash, always pull latest
if ( "$1" == "latest" ) then
  ssh ${ssh_loc} "cd $source_dir; ./git pull origin master"
  set version = `git rev-parse --short HEAD`
else if ( "$1" == "" ) then
  set version = `git rev-parse --short HEAD`
else
  ssh ${ssh_loc} "cd $source_dir; git fetch"
  set version = "$1"
  git checkout $version
  if ( $? != 0 ) then
    echo "GIT Checkout failed, please check version/hash"
    exit 1
  endif
endif

set source_dir = "${source_base}/${module}"
set build_dir = "${build_base}/${module}"
set install_dir = "${install_base}/${module}/${version}"
set lmod_dir = "${lmod_base}/${module}"
set lmod_file = "${lmod_dir}/${version}.lua"

echo "Building ${module}/${version}, log at $install_dir/build.log"

mkdir -m 0755 -p "${install_dir}"
mkdir -m 0755 -p "${install_dir}/lib/${python_small_ver}/site-packages"

setenv PYTHONPATH "${PYTHONPATH}:${install_dir}/lib/${python_small_ver}/site-packages"
python setup.py install --prefix=$install_dir >& $install_dir/build.log
if ( $? != 0 ) then
  echo "python setup.py install install failed: see ${install_dir}/build.log"
  exit 1
endif

popd

if ( ! ( -d ${lmod_dir} ) ) then
  mkdir -p $lmod_dir
endif

if ( -f ${lmod_file} ) then
  rm -f ${lmod_file}
endif

# use the template
cp common.lua.${gcc_ver} $lmod_file

# add custom rules to the LUA
echo 'setenv("PSRHOME_SIGPYPROC_PATH",pkg)' >> $lmod_file
echo "append_path('PYTHONPATH',pathJoin(pkg, 'lib', '${python_small_ver}', 'site-packages'))" >> $lmod_file

# determine the LD_LIBRARY_PATH automaticall
echo "ls -1d ${install_dir}/lib/${python_small_ver}/site-packages/*/libSigPyProc.*so"
set so_file = `ls -1d ${install_dir}/lib/${python_small_ver}/site-packages/*/libSigPyProc.*so`
set lib_dir = `dirname $so_file`
set egg_name = `basename $lib_dir`
echo 'append_path("LD_LIBRARY_PATH",pathJoin(pkg, "lib/${python_small_ver}/site-packages","'$egg_name'"))' >> $lmod_file

# add dependencies
foreach dep ( $deps )
  echo depends_on\(\"$dep\"\) >> $lmod_file
end
